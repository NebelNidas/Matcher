plugins {
	id 'java-library'
	id 'application'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '7.1.2'
	id 'de.jjohannes.extra-java-module-info' version '0.15'
}

repositories {
    mavenCentral()
    maven {
        name "Fabric"
        url 'https://maven.fabricmc.net/'
    }
	maven {
		name "Quilt"
		url 'https://maven.quiltmc.org/repository/release/'
	}
}

archivesBaseName = 'matcher'
group = 'net.fabricmc'

sourceSets {
    main {
        java
		resources
    }
}

java {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11

	if (!JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_11)) {
		toolchain {
			languageVersion = JavaLanguageVersion.of(11)
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 11
}

dependencies {
    implementation "com.github.javaparser:javaparser-core:3.24.2"
    implementation "net.fabricmc:mapping-io:0.3.0"
	implementation "org.bitbucket.mstrobel:procyon-compilertools:0.6.0"
	implementation "org.ow2.asm:asm:9.3"
	implementation "org.ow2.asm:asm-commons:9.3"
	implementation "org.ow2.asm:asm-tree:9.3"
	implementation "org.ow2.asm:asm-util:9.3"
	implementation "org.benf:cfr:0.152"
	implementation "org.quiltmc:quiltflower:1.8.1"

	// JavaFX for all platforms (needed for cross-platform fat jar)
	implementation "org.openjfx:javafx-base:11.0.2:win"
	implementation "org.openjfx:javafx-base:11.0.2:mac"
	implementation "org.openjfx:javafx-base:11.0.2:linux"
	implementation "org.openjfx:javafx-graphics:11.0.2:win"
	implementation "org.openjfx:javafx-graphics:11.0.2:mac"
	implementation "org.openjfx:javafx-graphics:11.0.2:linux"
	implementation "org.openjfx:javafx-controls:11.0.2:win"
	implementation "org.openjfx:javafx-controls:11.0.2:mac"
	implementation "org.openjfx:javafx-controls:11.0.2:linux"
	implementation "org.openjfx:javafx-web:11.0.2:win"
	implementation "org.openjfx:javafx-web:11.0.2:mac"
	implementation "org.openjfx:javafx-web:11.0.2:linux"
	runtimeOnly "org.openjfx:javafx-media:11.0.2:win"
	runtimeOnly "org.openjfx:javafx-media:11.0.2:mac"
	runtimeOnly "org.openjfx:javafx-media:11.0.2:linux"
}

extraJavaModuleInfo {
	automaticModule("mapping-io-0.3.0.jar", "net.fabricmc.mappingio")
	automaticModule("cfr-0.152.jar", "cfr")
	automaticModule("procyon-compilertools-0.6.0.jar", "procyon.compilertools")
	automaticModule("procyon-core-0.6.0.jar", "procyon.core")
	automaticModule("quiltflower-1.8.1.jar", "org.quiltmc.quiltflower")
}

application {
	mainModule = 'matcher'
	mainClass = 'matcher.Main'
}

build.dependsOn(shadowJar)

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
}